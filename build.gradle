buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.configFile = { fileName ->
        rootProject.file("gradle/$fileName")
    }

    def versionMajor = 2
    def versionMinor = 0
    def versionBuild = 4
    ext.playPublishTrack = 'alpha'

    ext.versions = [
            compileSdk : 30,
            minSdk     : 19,
            versionCode: versionMajor * 10000 + versionMinor * 100 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",
    ]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
        classpath 'com.github.triplet.gradle:play-publisher:3.0.0'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'com.gradleup:static-analysis-plugin:1.3'
        classpath 'com.google.gms:google-services:4.3.4'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
